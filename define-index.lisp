(in-package #:fact-base)

(defun make-index-fn (ix-name fst snd)
  (with-gensyms (ix)
    `(defmethod index-by ((lookup (eql ,ix-name)) (state index) a b c)
       (declare (ignorable a b c))
       (let ((,ix (gethash ,ix-name (table state))))
	 ,(if snd
	      `(gethash ,snd (gethash ,fst ,ix))
	      `(gethash ,fst ,ix))))))

(defun make-insert-fn (ix-name fst snd)
  (with-gensyms (lookup state fact ix)
    `(defmethod insert-ix ((,lookup (eql ,ix-name)) (,state index) (,fact list))
       (destructuring-bind (a b c) ,fact
	 (declare (ignorable a b c))
	 (let ((,ix (gethash ,ix-name (table ,state))))
	   ,@(if snd
		 `((unless (gethash ,fst ,ix)
		     (setf (gethash ,fst ,ix) (make-hash-table :test 'equal)))
		   (push ,fact (gethash ,snd (gethash ,fst ,ix))))
		 `((push
		    ,fact
		    (gethash ,fst ,ix)))))))))

(defun make-remove-fn (ix-name fst snd)
  (with-gensyms (lookup state fact ix)
    `(defmethod remove-ix ((,lookup (eql ,ix-name)) (,state index) (,fact list))
       (destructuring-bind (a b c) ,fact
	 (declare (ignorable a b c))
	 (let ((,ix (gethash ,ix-name (table ,state))))
	   (awhen (gethash ,fst ,ix)
	     ,@(if snd
		   `((when (gethash ,snd it)
		       (setf (gethash ,snd it) (remove ,fact (gethash ,snd it) :test #'equal)))
		     (unless (car (gethash ,snd it))
		       (remhash ,snd it))
		     (when (= 0 (hash-table-count (gethash ,fst ,ix)))
		       (remhash ,fst ,ix)))
		   `((setf (gethash ,fst ,ix) (remove ,fact it :test #'equal))
		     (unless (car (gethash ,fst ,ix))
		       (remhash ,fst ,ix))))))))))

(defmacro define-index (&rest order)
  (let ((ix-name (intern (format nil "狺矧溴颟弘妁黠蜾┅ㄦ篝ㄦ轵篝矧溴颟箢箦泔钿矧溴颟┅啜痱镧磲脲轭溴骖轼钺礤骟箢洎磲脲轭箦螋骖轼钺礤骟箢洎磲脲蝈盹鲥骖轼钺礤骟箢洎┅